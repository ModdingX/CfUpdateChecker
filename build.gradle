plugins {
    id 'java-library'
    id 'application'
    id 'maven-publish'
}

group = 'org.moddingx'

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

repositories {
    mavenCentral()
    maven { url = 'https://maven.moddingx.org/' }
}

dependencies {
    implementation 'org.moddingx:LauncherLib:2.1.0'
    implementation 'org.moddingx:CurseWrapper:5.0'
    implementation 'org.apache.commons:commons-text:1.12.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'org.jsoup:jsoup:1.17.2'
    implementation 'com.moandjiezana.toml:toml4j:0.7.2'
    implementation 'com.google.guava:guava:33.2.1-jre'
}

application.mainClass = 'org.moddingx.updatecheckergenerator.Main'

jar {
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
}

task fatjar(type: Jar) {
    archiveClassifier = 'fatjar'
    manifest = jar.manifest
    with jar
}
build.dependsOn fatjar

task configureFatJar {
    doLast {
        configurations.runtimeClasspath.each { dep ->
            fatjar.from(project.zipTree(dep)) {
                exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
    outputs.upToDateWhen { false }
}
fatjar.dependsOn configureFatJar

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'moddingx'
            url 'https://maven.moddingx.org/release'
            credentials(PasswordCredentials)
        }
    }
}
